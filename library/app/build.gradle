apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile 'com.google.firebase:firebase-database:10.0.1'
    compile 'com.google.firebase:firebase-auth:10.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
}

android.libraryVariants
publishing {
    publications {
        maven(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            artifactId = 'rxfiredroid'
            groupId = 'de.goddchen.android'
            version = '0.1'
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")

    publications = ['maven']

    dryRun = false
    publish = true
    override = true

    pkg {
        repo = 'RxFireDroid'
        name = 'RxFireDroid'
        userOrg = 'goddchen2'
        desc = 'Rx (Version 2!) wrapper for the Firebase Android library'
        issueTrackerUrl = 'https://github.com/Goddchen/RxFireDroid/issues'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/Goddchen/RxFireDroid.git'
        websiteUrl = 'https://github.com/Goddchen/RxFireDroid'
        labels = ['android', 'rxjava', 'rx', 'rxjava2']
        publicDownloadNumbers = true
        githubRepo = 'Goddchen/RxFireDroid'

        version {
            name = '0.1'
        }
    }
}

apply plugin: 'com.google.gms.google-services'